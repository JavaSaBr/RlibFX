buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.0'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'idea'
apply plugin: 'org.junit.platform.gradle.plugin'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

group = 'com.spaceshift'
version = '5.0.4'

sourceCompatibility = 1.10
targetCompatibility = 1.10

compileTestJava {
    sourceCompatibility = 1.10
    targetCompatibility = 1.10
    options.compilerArgs += '-parameters'
}

ext.junitPlatformVersion = "1.0.0"
ext.junitJupiterVersion = "5.0.0"
ext.log4jVersion = '2.6.2'
ext.bintrayVersion = version

junitPlatform {
    filters {
        engines {
        }
        tags {
        }
    }
    logManager 'org.apache.logging.log4j.jul.LogManager'
}

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        url 'https://jitpack.io'
    }
    maven {
        url  "https://dl.bintray.com/javasabr/maven"
    }
}

publishing {
    publications {
        RlibFXArtifact(MavenPublication) {
            from components.java
            groupId group
            artifactId 'rlib.fx'
            version bintrayVersion
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

bintray {
    user = System.getProperty('bintrayUser')
    key = System.getProperty('bintrayKey')
    publications = ['RlibFXArtifact']
    pkg {
        repo = 'maven'
        name = 'com.spaceshift.rlib.fx'
        licenses = ['Apache-2.0']
        websiteUrl = 'https://github.com/JavaSaBr/RlibFX'
        vcsUrl = 'https://github.com/JavaSaBr/RlibFX.git'
        issueTrackerUrl = 'https://github.com/JavaSaBr/RlibFX/issues'
        githubRepo = 'JavaSaBr/RlibFX'
        githubReleaseNotesFile = 'README.md'
        version {
            name = bintrayVersion
            desc = bintrayVersion
            released  = new Date()
        }
    }
}

dependencies {
    compile 'com.spaceshift:rlib.common:7.0.0'
    compile group: 'org.jetbrains', name: 'annotations', version: '15.0'

    testRuntime "org.junit.platform:junit-platform-engine:$junitPlatformVersion"

    testCompile "org.junit.jupiter:junit-jupiter-api:$junitJupiterVersion"
    testRuntime "org.junit.jupiter:junit-jupiter-engine:$junitJupiterVersion"

    testRuntime "org.apache.logging.log4j:log4j-core:$log4jVersion"
    testRuntime "org.apache.logging.log4j:log4j-jul:$log4jVersion"

    // Only needed to run tests in an (IntelliJ) IDE(A) that bundles an older version
    testCompile "org.junit.platform:junit-platform-launcher:$junitPlatformVersion"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.7'
}